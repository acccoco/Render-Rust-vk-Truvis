#pragma once

#ifdef __SLANG__
// 注意：需要 nonuniform，表示同一个 wavefront 的下标是不同的
[[vk::binding(0, 0)]]
Sampler2D<float4> bindless_textures[];

[[vk::binding(1, 0)]]
RWTexture2D<float4> bindless_images[];
#endif

const static int INVALID_TEX_ID = -1;
const static float4 INVALID_TEX_COLOR = float4(1.0f, 0.0f, 1.0f, 1.0f);

struct TextureHandle
{
    /// 纹理的索引
    int index;

#ifdef __SLANG__
    [ForceInline]
    float4 sample(float2 uv)
    {
        if (index == INVALID_TEX_ID)
        {
            return INVALID_TEX_COLOR;
        }
        else
        {
            return bindless_textures[NonUniformResourceIndex(index)].Sample(uv);
        }
    }
    float4 sample_level(float2 uv, float level)
    {
        if (index == INVALID_TEX_ID)
        {
            return INVALID_TEX_COLOR;
        }
        else
        {
            return bindless_textures[NonUniformResourceIndex(index)].SampleLevel(uv, level);
        }
    }

    [ForceInline]
    bool is_valid()
    {
        return index != INVALID_TEX_ID;
    }
#endif
};

struct ImageHandle
{
    /// 纹理的索引
    int index;

#ifdef __SLANG__
    [ForceInline]
    void store(uint2 coord, float4 value)
    {
        bindless_images[NonUniformResourceIndex(index)].Store(coord, value);
    }

    [ForceInline]
    float4 load(uint2 coord)
    {
        return bindless_images[NonUniformResourceIndex(index)].Load(coord);
    }
#endif
};
