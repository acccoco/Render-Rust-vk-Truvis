#pragma once
#include "common.slangi"

struct Random
{
    uint rng_stage;
    __init(uint a, uint b)
    {
        rng_stage = tea(a, b);
    }

    /// 获得下一个随机数，uniform[0, 1]
    [mutating]
    float next()
    {
        return rnd(rng_stage);
    }

    // 生成随机数的初始种子
    // Generate a random unsigned int from two unsigned int values, using 16 pairs
    // of rounds of the Tiny Encryption Algorithm. See Zafar, Olano, and Curtis,
    // "GPU Random Numbers via the Tiny Encryption Algorithm"
    static uint tea(uint v0, uint v1)
    {
        uint s0 = 0;

        for (uint n = 0; n < 16; n++)
        {
            s0 += 0x9e3779b9;
            v0 += ((v1 << 4) + 0xa341316c) ^ (v1 + s0) ^ ((v1 >> 5) + 0xc8013ea4);
            v1 += ((v0 << 4) + 0xad90777d) ^ (v0 + s0) ^ ((v0 >> 5) + 0x7e95761e);
        }

        return v0;
    }

    // Generate a random unsigned int in [0, 2^24) given the pre_seedious RNG state
    // using the Numerical Recipes linear congruential generator
    static uint lcg(inout uint pre_state)
    {
        uint LCG_A = 1664525u;
        uint LCG_C = 1013904223u;
        pre_state = (LCG_A * pre_state + LCG_C);
        return pre_state & 0x00FFFFFF;
    }

    // Generate a random float in [0, 1) given the pre_seedious RNG state
    static float rnd(inout uint pre_state)
    {
        return (float(lcg(pre_state)) / float(0x01000000));
    }

    /// 生成符合高斯分布的随机数：中心为 0，标准差为 1
    static float2 rand_gaussian(inout uint pre_state)
    {
        // Almost uniform in (0,1] - make sure the value is never 0:
        const float u1 = max(1e-38, rnd(pre_state));
        const float u2 = rnd(pre_state); // In [0, 1]
        // log = log_e
        const float r = sqrt(-2.0 * log(u1));
        const float theta = 2 * M_PI * u2; // Random in [0, 2pi]
        return r * float2(cos(theta), sin(theta));
    }
};
